dnl Modified for CYGWIN support
dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/main.cpp)
AM_INIT_AUTOMAKE(stellarium,0.4.9)

dnl Check system type
AC_CANONICAL_SYSTEM

dnl Check for compilers.
AC_PROG_CC
AC_PROG_CXX

CFLAGS="-Wall -O3 -ffast-math -fexpensive-optimizations"
CXXFLAGS="-O3 -ffast-math -fexpensive-optimizations"

dnl Checks for programs.
AC_PROG_CPP
AC_PROG_CXXCPP
AC_PROG_INSTALL
AC_PROG_RANLIB

dnl Check for X11.
AC_PATH_XTRA
LIBS="$LIBS $X_LIBS"

AC_C_BIGENDIAN
AC_CHECK_HEADER(byteswap.h)

dnl GL and GLUT libs

case "$host" in 
*-*-darwin*)
	AC_DEFINE_UNQUOTED(MACOSX,1)
	LIBS="$LIBS -framework GLUT -framework OpenGL -lobjc"
	AC_CHECK_HEADER(OpenGL/gl.h,,AC_MSG_ERROR(gl.h not found - please install OpenGL))
	AC_CHECK_HEADER(OpenGL/glu.h,,AC_MSG_ERROR(glu.h not found - please install GLU))
	AC_CHECK_HEADER(GLUT/glut.h,,AC_MSG_ERROR(glut.h not found - please install GLUT))
;;

*-*-cygwin*)
	LIBS="$LIBS -lopengl32 -lglu32 -lglut32"
	AC_CHECK_HEADER(GL/gl.h,,AC_MSG_ERROR(gl.h not found - please install OpenGL))
	AC_CHECK_HEADER(GL/glu.h,,AC_MSG_ERROR(glu.h not found - please install GLU))
	AC_CHECK_HEADER(GL/glut.h,,AC_MSG_ERROR(glut.h not found - please install GLUT))
;;

*)
	AC_CHECK_HEADER(GL/gl.h,,AC_MSG_ERROR(gl.h not found - please install OpenGL- or Mesa-devel))
	AC_CHECK_HEADER(GL/glu.h,,AC_MSG_ERROR(glu.h not found - please install GLU- or MesaGLU-devel))
	AC_CHECK_HEADER(GL/glut.h,,AC_MSG_ERROR(glut.h not found - please install GLUT- or MesaGLUT-devel))

	AC_CHECK_LIB(GL,glEnable)
   	if test "$ac_cv_lib_GL_glEnable" = no ; then
    	AC_CHECK_LIB(MesaGL,glEnable,,AC_MSG_ERROR(GL not found - please install GL or MesaGL))
   	fi

   	AC_CHECK_LIB(GLU,gluPerspective)
   	if test "$ac_cv_lib_GLU_gluPerspective" = no ; then
    	AC_CHECK_LIB(MesaGLU,gluPerspective,,AC_MSG_ERROR(GLU not found - please install GLU or MesaGLU))
   	fi

   	AC_CHECK_LIB(glut,glutMainLoop,,AC_MSG_ERROR(GLUT not found - please install GLUT))

dnl LIBS="$LIBS -lGL -lGLU -lglut"
;;
esac

dnl Checks for library functions.
AC_CHECK_FUNCS(toupper strcasecmp strdup)
AC_CHECK_LIB(m,fabs,LIBS="$LIBS -lm",AC_MSG_RESULT(math library not found))

dnl assign the package directory variable
if test "$prefix" = "NONE" ; then
   prefix=$ac_default_prefix
fi
PKGDATADIR=`eval echo "${datadir}/$PACKAGE"`
AC_DEFINE_UNQUOTED(CONFIG_DATA_DIR, "$PKGDATADIR")

AC_OUTPUT(Makefile src/Makefile src/astrolib/Makefile src/glpng/Makefile config/Makefile data/Makefile textures/Makefile textures/landscapes/Makefile)
